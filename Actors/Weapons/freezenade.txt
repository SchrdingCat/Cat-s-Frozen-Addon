ACTOR Freezenades : BrutalWeapon
{
	Scale 0.7
	Weapon.AmmoUse 1
	Weapon.AmmoType "FreezenadeAmmo"
	Weapon.SelectionOrder 3800
	-WEAPON.NOAUTOFIRE
	+WEAPON.ALT_USES_BOTH
	-WEAPON.AMMO_CHECKBOTH
	-WEAPON.AMMO_OPTIONAL
	-WEAPON.ALT_AMMO_OPTIONAL
	Inventory.PickupSound "AmmoAPIN"
	Inventory.Pickupmessage "You've got Freezenades !"
	Inventory.RestrictedTo BDoomer, Doomer2, Doomer3, TacticalDoomer
	Obituary "\cr%o\c- got frozen by \cr%k's \c-Freezenade!"
	Tag "Freezenades"
	States
	{
	KickFlash: 
		TNT1 A 0 A_JumpIfNoAmmo(3)
		VGRN Q 0 A_JumpIfInventory("IsPlayingAsPurist",1, 2)
		FGDW Q 0
		"####" "#" 18
		stop
	SlideFlash: 
		TNT1 A 0 A_JumpIfNoAmmo(3)
		VGRN Q 0 A_JumpIfInventory("IsPlayingAsPurist",1, 2)
		FGDW Q 0
		"####" "#" 35
		stop
	SlideFlashEnd: 
		TNT1 A 0 A_JumpIfNoAmmo(3)
		VGRN Q 0 A_JumpIfInventory("IsPlayingAsPurist",1, 2)
		FGDW Q 0
		"####" "#" 9
		stop
	SlideFlashEndEarly: 
		TNT1 A 0 A_JumpIfNoAmmo(3)
		VGRN Q 0 A_JumpIfInventory("IsPlayingAsPurist",1, 2)
		FGDW Q 0
		"####" "#" 6
		stop

	Spawn:
		FGND H -1
		Stop

	Select:
		TNT1 A 0 A_Giveinventory("GoSpecial",1)
		TNT1 A 0 A_Giveinventory("SwitchtoFist",1)
		TNT1 A 0 A_Giveinventory("FistsSelected",1)
		TNT1 A 0 A_Giveinventory("GoSpecial",1)
		TNT1 A 0 A_GiveInventory("TokenCleaner")
		TNT1 A 0 A_SetCrosshair(41)
		TNT1 A 0 A_GunFlash
		TNT1 A 1
	SelectAnimation:
		VGRN Q 0 A_JumpIfInventory("IsPlayingAsPurist", 1, "SelectPurist")
		TNT1 A 0 A_JumpIfInventory("FreezenadeAmmo",1,"SelectNormal")
		Goto Ready
	SelectNormal:
		VGRN Q 0 A_JumpIfInventory("IsPlayingAsPurist",1, 2)
		FGDW Q 0
		"####" VWX 1 A_WeaponReady(WRF_NOFIRE)
	Ready:
	Ready3:
		TNT1 A 0 A_JumpIfNoAmmo(3)
		VGRN Q 0 A_JumpIfInventory("IsPlayingAsPurist",1, 2)
		FGDW Q 0
		"####" "#" 1 A_WeaponReady
		"####" "#" 0 A_JumpIfInventory("TossGrenade",1,"TossGrenade")
		"####" "#" 0 A_JumpIfInventory("Kicking",1,"DoKick")
		"####" "#" 0 A_JumpIfInventory("Taunting",1,"Taunt")
		"####" "#" 0 A_JumpIfInventory("Salute1", 1, "Salute")
		"####" "#" 0 A_JumpIfInventory("Salute2", 1, "Salute")
		"####" "#" 0 A_JumpIfInventory("IsRunning",1,"StartSprint")
		"####" A 0 A_JumpIfInventory("StartDualWield",1,"DualWield")
		Loop
	
	/*SelectPurist:
        "####" "#" 0 A_JumpIfInventory("PuristHandGrenades", 1,3)    
        "####" "#" 0 A_Giveinventory("PuristHandGrenades",1)
        "####" "#" 0 A_SelectWeapon("PuristHandGrenades")
        "####" "####" 0 A_Raise
        "####" "#" 1 A_Raise
        "####" "#" 0 A_Takeinventory("HandGrenades",1)
        Goto SelectPurist+1*/

	Deselect:
		TNT1 A 0 A_JumpIfInventory("FreezenadeAmmo",1,"DeselectNormal")
		TNT1 A 0 A_TakeInventory("HasFreezerWeapon", 1)
		TNT1 AAAAAAAAAAAAAAAA 0 A_Lower
		TNT1 A 1 A_Lower
		wait
	DeselectNormal:
		VGRN Q 0 A_JumpIfInventory("IsPlayingAsPurist",1, 2)
		TNT1 A 0 A_TakeInventory("HasFreezerWeapon", 1)
		FGDW Q 0
		"####" XWV 1
		TNT1 AAAAAAAAAAAAAAAA 0 A_Lower
		TNT1 A 1 A_Lower
		Wait
	
	User1:
	TossGrenade:
	Fire:
		"####" "#" 0 A_TakeInventory("TossGrenade", 1)
		"####" "#" 0 A_Giveinventory("HasFreezerWeapon",1)
		"####" RSTUDEF 1
		"####" A 0 A_PLaySound ("Grenade/Pin", 5)
		"####" FGHIJK 1
	Hold:
		TNT1 A 1
		TNT1 A 1 A_Refire
		TNT1 A 0 A_PLaySound ("Grenade/Toss", 5)
		//TNT1 A 0 A_TakeInventory("FreezenadeAmmo", 1,TIF_NOTAKEINFINITE)
		TNT1 A 0 A_TakeInventory("FiredGrenade", 1)
		VGRN Q 0 A_JumpIfInventory("IsPlayingAsPurist",1, 2)
		FGDW Q 0
		"####" A 0 A_Quake(3,4,0,12,"")
		"####" LM 1
		"####" A 0 A_ThrowGrenade("Freezenade",5,45,5,1)
		"####" NOP 1
		TNT1 A 1
		TNT1 A 0 A_JumpIfInventory("TossGrenade", 1, "TossGrenade")
		TNT1 A 0 A_TakeInventory("TossGrenade", 1)
		VGRN Q 0 A_JumpIfInventory("IsPlayingAsPurist",1, 2)
		FGDW Q 0
		"####" VWX 1
		Goto Ready

	AltFire:
		"####" "#" 0 A_TakeInventory("TossGrenade", 1)
		"####" "#" 0 A_Giveinventory("HasFreezerWeapon",1)
		VGRN Q 0 A_JumpIfInventory("IsPlayingAsPurist",1, 2)
		FGDW Q 0
		"####" RSTUDEF 1
		"####" A 0 A_PLaySound ("Grenade/Pin", 5)
		"####" FGHIJK 1
	AltHold:
		TNT1 A 1
		TNT1 A 1 A_Refire
		TNT1 A 0 A_PLaySound ("Grenade/Toss", 5)
		TNT1 A 0 A_TakeInventory("FreezenadeAmmo", 1,TIF_NOTAKEINFINITE)
		TNT1 A 0 A_TakeInventory("FiredGrenade", 1)
		VGRN Q 0 A_JumpIfInventory("IsPlayingAsPurist",1, 2)
		FGDW Q 0
		"####" LM 1
		"####" A 0 A_FireCustomMissile("FreezenadeAlt", 0, 0, 0, 0, 0, 0)
		"####" NOP 1
		TNT1 A 1
		TNT1 A 0 A_JumpIfInventory("TossGrenade", 1, "TossGrenade")
		VGRN Q 0 A_JumpIfInventory("IsPlayingAsPurist",1, 2)
		FGDW Q 0
		"####" VWX 1
		Goto Ready
	
	DualWield:
		"####" "#" 0 A_Takeinventory("StartDualWield",1)
		"####" "#" 0 A_JumpIfInventory("FreezenadeSelected", 1, "DeselectFreezenade")
	SelectFreezenade:
		"####" "#" 0 A_GiveInventory("FreezenadeSelected")
		"####" "#" 0 A_Print("Freezenades Selected")
		Goto Ready
	DeselectFreezenade:
		"####" "#" 0 A_TakeInventory("FreezenadeSelected")
		"####" "#" 0 A_Print("Grenades Selected")
		Goto Ready
		
	StartSprint:
		"####" "#" 1 A_WeaponReady(WRF_NOFIRE)
		"####" "#" 0 A_JumpIfInventory("UsedStamina", 40, "StopSprintTired")
	Sprinting:
		"####" "#" 0 offset(-9,32)
		"####" "#" 0 offset(-9,32) A_JumpIfInventory("IsStandingStill", 1, "Ready3")
		"####" "#" 0 offset(-9,32) A_JumpIfInventory("UsedStamina", 100, "StopSprintTired")
		"####" "#" 0 offset(-9,32) ACS_ExecuteAlways(852, 0, 0, 0, 0)//Makes player faster.
		"####" "#" 0 offset(-9,32) A_JumpIfInventory("PowerStrength", 1, 2)
		"####" "#" 0 offset(-9,32) A_GiveInventory("UsedStamina", 2)
		"####" "#" 0 offset(-9,32)
		"####" "#" 1 offset(-9,34) A_SetPitch(pitch -0.5)
		"####" "#" 1 offset(-6,36) A_SetPitch(pitch -0.5)
		"####" "#" 1 offset(-3,38) A_SetPitch(pitch -0.5)
		"####" "#" 1 offset(0,38) A_SetPitch(pitch -0.5)
		"####" "#" 1 offset(3,36) A_SetPitch(pitch -0.5)
		"####" "#" 1 offset(6,34) A_SetPitch(pitch -0.5)
		"####" "#" 1 offset(9,32) A_SetPitch(pitch -0.5)
		"####" "#" 0 offset(9,32) A_WeaponReady(WRF_NOBOB)
		"####" "#" 0 A_JumpIfInventory("Kicking",1,"DoKick")
		"####" "#" 0 A_JumpIfInventory("Taunting",1,"Taunt")
		"####" "#" 0 offset(-9,32) A_GiveInventory("UsedStamina", 3)
		"####" "#" 0 offset(9,33) A_SpawnItemEx("FootStep", 0, 0, 2, 0, 0, -4)
		"####" "#" 1 offset(9,34) A_SetPitch(pitch +0.5)
		"####" "#" 1 offset(6,36) A_SetPitch(pitch +0.5)
		"####" "#" 1 offset(3,38) A_SetPitch(pitch +0.5)
		"####" "#" 1 offset(0,38) A_SetPitch(pitch +0.5)
		"####" "#" 1 offset(-3,36) A_SetPitch(pitch +0.5)
		"####" "#" 1 offset(-6,34) A_SetPitch(pitch +0.5)
		"####" "#" 1 offset(-9,32) A_SetPitch(pitch +0.5)
		"####" "#" 0 offset(-9,32) A_WeaponReady(WRF_NOBOB)
		"####" "#" 0 offset(-9,32) A_JumpIfInventory("IsRunning", 1, "Sprinting")
		Goto StopSprint

	StopSprintTired:
		"####" "#" 1
		"####" "#" 0 ACS_ExecuteAlways(853, 0, 0, 0, 0)//Makes player slower.
		"####" "#" 0 A_PlaySound("Tired", 2)
		"####" "#" 0 A_TakeInventory("UsedStamina", 2)
		"####" "#" 5 A_WeaponReady
		"####" "#" 0 A_TakeInventory("UsedStamina", 2)
		"####" "#" 5 A_WeaponReady
		"####" "#" 0 A_TakeInventory("UsedStamina", 2)
		"####" "#" 5 A_WeaponReady
		"####" "#" 0 A_TakeInventory("UsedStamina", 2)
		"####" "#" 5 A_WeaponReady
		"####" "#" 0 A_TakeInventory("UsedStamina", 2)
		"####" "#" 5 A_WeaponReady
		Goto Ready

	StopSprint:
		"####" "#" 1
		"####" "#" 0 A_JumpIfInventory("UsedStamina", 60, "StopSprintTired")
		"####" "#" 0 ACS_ExecuteAlways(853, 0, 0, 0, 0)//Makes player slower.
		Goto Ready
	}
}

ACTOR Freezenade : HandGrenade
{
	
	States
	{
	Spawn:
		FGND ABCDEFGH 2
		TNT1 A 0 A_GiveInventory("GrenadeTime", 1)
		TNT1 A 0 A_JumpIfInventory("GrenadeTime", 3, "Explode")
		Loop

	Death:
		TNT1 A 0
		TNT1 A 0 A_AlertMonsters(200)
		FGND D 16
		TNT1 A 0 A_GiveInventory("GrenadeTime", 1)
		TNT1 A 0 A_JumpIfInventory("GrenadeTime", 3, "Explode")
		Loop

	Explode:
		TNT1 A 0 A_NoBlocking
		TNT1 A 0 A_TakeFromTarget("HasFreezerWeapon",1) 
		TNT1 A 0 A_SpawnItemEx("FreezenadeExplosion", 0, 0 ,2.5)
		TNT1 AA 0
		Stop
	}
}


Actor FakeFreezenade: Freezenade
{
	SeeSound "dsempty"
	Damage (random (5, 5))
	States
	{
	Spawn:
		TNT1 ABCDEFGH 2
		Loop

	Death:
		TNT1 A 0
		Stop
	}
}

Actor FreezenadeDroped: Freezenade
{
	Speed 4
}

Actor FreezenadeAlt: Freezenade
{
	Speed 8
}

actor FreezenadeExplosion
{
+NOBLOCKMAP
+MISSILE
+DONTSPLASH
+NOGRAVITY
Damagetype "Ice"
DeathSound "FRZFIRE2"
Height 32
RenderStyle Add
Alpha 0.95
Scale 2.5
+NODAMAGETHRUST
+FORCEXYBILLBOARD
-FORCERADIUSDMG
//MissileHeight 8
Decal "IceScorch"

States    {
    Spawn:
		TNT1 A 0
		TNT1 A 0 A_SpawnItemEx ("BluePlasmaParticleSpawner", 0, 0, -20)
		TNT1 A 0 A_SpawnItemEx ("BlueFlareSpawn",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION,0)
	    TNT1 A 0 A_SpawnItemEx ("BlueFlare3",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION,0)
		TNT1 A 0 A_SpawnItemEX("HorizontalShockwave2",0,0,10,0,0,0,0,SXF_NOCHECKPOSITION,0)
		TNT1 A 0 A_CustomMissile ("ExplosionSmokeHD", 0, 0, random (0, 360), 2, random (0, 90))
        TNT1 AAAAAAAAAA 0 A_CustomMissile ("BluePlasmaFire", 0, 0, random (0, 360), 2, random (0, 360))
		TNT1 AAAAAA 0 A_CustomMissile ("BluePlasmaParticle", 0, 0, random (0, 360), 2, random (0, 90))
		TNT1 AAAAA 0 A_CustomMissile ("BluePlasmaParticle", 0, 0, random (0, 360), 2, random (0, 90))
		TNT1 AAAA 0 A_CustomMissile ("RealisticFireSparks1Blue", 0, 0, random (0, 360), 2, random (0, 360))
	    TNT1 AAAA 0 A_CustomMissile ("RealisticFireSparks1Blue", 0, 0, random (0, 360), 2, random (0, 360))
		TNT1 AAA 0 A_CustomMissile ("BigPlasmaParticleX", 0, 0, random (0, 360), 2, random (0, 360))
		TNT1 AAA 0 A_CustomMissile ("BigPlasmaParticleX", 0, 0, random (0, 360), 2, random (0, 360))
		TNT1 A 0 A_SpawnItemEx ("DetectFloorCraterBigIce",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION,0)
		TNT1 A 0 A_SpawnItemEx ("DetectCeilCraterIce",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION,0)
		BXPL A 0 Radius_Quake (2, 24, 0, 15, 0)
        TNT1 A 0 A_ALertMonsters
		TNT1 A 0 A_PlaySound("FRZFIRE2", 3, 3)
		TNT1 A 0 A_SpawnItem("CryoSmoke")

		BXPL A 0 Bright A_ChangeFlag("ICEDAMAGE", 1)
		BXPL A 0 Bright A_ChangeFlag("NODAMAGETHRUST", 0)
		BXPL A 0 Bright A_Explode(30*random(5,6),500)
		
		TNT1 A 0 A_SpawnItemEx ("DetectFloorIce",0,0,1,0,0,0,0,SXF_NOCHECKPOSITION,0)
		TNT1 A 0 A_SpawnItemEx ("DetectFloorIce2",0,0,1,0,0,0,0,SXF_NOCHECKPOSITION,0)
		TNT1 A 0 A_SpawnItemEx ("DetectCeilIce",0,0,1,0,0,0,0,SXF_NOCHECKPOSITION,0)
		
		BXPL A 0 A_SpawnItemEx("SmallFreezerBall", -10, 0, -1, random(-20,20), random(-20,20), random(-20,40))
		BXPL A 0 A_SpawnItemEx("SmallFreezerBall", -8, 0, -1, random(-20,20), random(-20,20), random(-20,40))
		BXPL A 0 A_SpawnItemEx("SmallFreezerBall", -6, 0, -1, random(10,90), random(-20,20), random(-20,40))
		BXPL A 0 A_SpawnItemEx("SmallFreezerBall", -4, 0, -1, random(-50,10), random(-20,10), random(-20,40))
		BXPL A 0 A_SpawnItemEx("SmallFreezerBall", -2, 0, -1, random(-20,30), random(-20,40), random(-20,40))
		BXPL A 0 A_SpawnItemEx("SmallFreezerBall", 0, 0, -1, random(-20,20), random(-20,30), random(-20,40))
		BXPL A 0 A_SpawnItemEx("SmallFreezerBall", 1, 0, -1, random(-10,20), random(-20,40), random(-20,40))
		BXPL A 0 A_SpawnItemEx("SmallFreezerBall", 2, 0, -1, random(-20,10), random(-20,20), random(-20,40))
		BXPL A 0 A_SpawnItemEx("SmallFreezerBall", 4, 0, -1, random(-20,30), random(-20,20), random(-20,40))
		BXPL A 0 A_SpawnItemEx("SmallFreezerBall", 6, 0, -1, random(-10,40), random(-20,40), random(-20,40))
		BXPL A 0 A_SpawnItemEx("SmallFreezerBall", 8, 0, -1, random(-30,20), random(-20,40), random(-20,40))
	    BXPL A 0 A_SpawnItemEx("SmallFreezerBall", 10, 0, -1, random(-25,25), random(-20,40), random(-20,40))
		BXPL AAAAAAAAAAA 0 Bright A_SpawnItemEx("CryoSmoke", 0, 0, 0, random(10, 30)*0.1, 0, random(0, 10)*0.1, random(1,360), SXF_CLIENTSIDE|SXF_NOCHECKPOSITION)
		BXPL AAAAAAAAAAA 0 Bright A_SpawnItemEx("CryoSmoke", 0, 0, 0, random(10, 30)*0.1, 0, random(0, 10)*0.1, random(1,360), SXF_CLIENTSIDE|SXF_NOCHECKPOSITION)
		BXPL AAAAA 0 Bright A_SpawnItemEx("CryoSmoke3", 0, 0, 0, random(10, 30)*0.04, 0, random(0, 10)*0.04, random(1,360), SXF_CLIENTSIDE|SXF_NOCHECKPOSITION,64)
		BXPL AAAAAAA 0 Bright A_SpawnItemEx("FreezerTrailSparksSmall", random(5,-5), random(5,-5), random(5,-5), random(10, 30)*0.04, 0, random(0, 10)*0.04, random(1,360), SXF_CLIENTSIDE|SXF_NOCHECKPOSITION,64)
		BXPL AAAAAAAAAAA 0 Bright A_SpawnItemEx("CryoSmoke", 0, 0, 0, random(10, 30)*0.1, 0, random(0, 10)*0.1, random(1,360), SXF_CLIENTSIDE|SXF_NOCHECKPOSITION)
		BXPL AAAAAA 0 Bright A_SpawnItemEx("CryoSmoke2", 0, 0, 0, random(10, 30)*0.04, 0, random(0, 10)*0.04, random(1,360), SXF_CLIENTSIDE|SXF_NOCHECKPOSITION,64)
		BXPL AAAAAA 0 Bright A_SpawnItemEx("CryoSmoke2", 0, 0, 0, random(10, 30)*0.04, 0, random(0, 10)*0.04, random(1,360), SXF_CLIENTSIDE|SXF_NOCHECKPOSITION,64)
		BXPL A 0 A_ChangeFlag("ICEDAMAGE", 0)
		BXPL AAAAAAAAAAAAAAAAAAAAAAA 0 BRIGHT A_SpawnItemEx("IceShardSmallSG",0,0,0,random(18,24),random(-18,18),random(-18,18),random(-45,45))
		BXPL ABCDEFGH 1 Bright
		BXPL A 0 A_ChangeFlag("ICEDAMAGE", 0)
		BXPL AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 BRIGHT A_SpawnItemEx("IceShardSmallSG",0,0,0,random(18,24),random(-18,18),random(-18,18),random(-45,45))
		TNT1 A 0 A_SpawnItemEX("HorizontalShockwave2",0,0,10,0,0,0,0,SXF_NOCHECKPOSITION,0)
		BXPL IJKLLM 1 Bright A_FadeOut(0.1)
	   TNT1 A 1
	   Stop
	   }
		
}
		

ACTOR PistolFromCryoRifle : BrutalWeapon
{
	Weapon.AmmoType1 "Clip1"
	Weapon.AmmoType2 "BDPistolAmmo"
	weapon.selectionorder 1900
	+WEAPON.CHEATNOTWEAPON
	+WEAPON.NO_AUTO_SWITCH
	+WEAPON.WIMPY_WEAPON
	-INVENTORY.UNDROPPABLE
	Inventory.PickupSound "Pistol/Draw"
	Inventory.PickupMessage "$GOTPISTOL"
	Obituary "$OBBD_PISTOL"
	Tag "$TAGBD_PISTOL"
	States
	{
	KickFlash:
		CPGG A 0 A_JumpIfInventory("BDPistolAmmo",1,"KickFlashLoaded")
		CPGG HIJK 1
		CPGG L 10
		CPGG KJIH 1
		stop
	SlideFlash:
		CPGG A 0 A_JumpIfInventory("BDPistolAmmo",1,"SlideFlashLoaded")
		CPGG HIJK 1
		CPGG L 35
		stop
	SlideFlashEnd:
		CPGG A 0 A_JumpIfInventory("BDPistolAmmo",1,"SlideFlashEndLoaded")
		CPGG L 5
		CPGG KJIH 1
		stop
	SlideFlashEndEarly: 
		CPGG A 0 A_JumpIfInventory("BDPistolAmmo",1,"SlideFlashEndEarlyLoaded")
		CPGG L 2
		CPGG KJIH 1
		stop

	KickFlashLoaded:
		CPGG CDEF 1
		CPGG G 10
		CPGG FEDC 1
		stop
	SlideFlashLoaded:
		CPGG CDEF 1
		CPGG G 35
		stop
	SlideFlashEndLoaded: 
		CPGG G 5
		CPGG FEDC 1
		stop
	SlideFlashEndEarlyLoaded: 
		CPGG G 2
		CPGG FEDC 1
		stop

	Select:
		TNT1 A 0 A_Giveinventory("GoSpecial",1)
		TNT1 A 0 A_GiveInventory("TokenCleaner")
		TNT1 A 0 A_SetCrosshair( CallACS("GetCrosshair", 1) )
		TNT1 A 0 A_GunFlash
		TNT1 A 0 A_JumpIfInventory("PistolSpecial", 1, "SelectPistol")
		TNT1 A 1
	SelectPistol:
		CPGG "#" 0 A_Takeinventory("PistolSpecial",1)
		CYGW BCDEF 1 A_WeaponReady(WRF_NOFIRE)
		CPGG F 0 A_PlaySound("Pistol/Draw",5)
		CPGG F 0 A_JumpIfInventory("BDPistolAmmo",1,1)
		Goto SelectPistolEmpty
		CPGS ABCD 1 A_WeaponReady(WRF_NOFIRE)
		CPGG "#" 0 A_Takeinventory("StartDualWield",1)
		Goto Ready

	SelectPistolEmpty:
		CPGS EFGH 1 A_WeaponReady(WRF_NOFIRE)
		CPGG "#" 0 A_Takeinventory("StartDualWield",1)
		Goto Ready
		
	Ready3:
	Ready:
		CPGG A 0 A_JumpIfInventory("BDPistolAmmo",1,2)
		CPGG B 0
		CPGG "#" 1 A_WeaponReady
		CPGG "#" 0 A_JumpIfInventory("ExecuteDownedEnemy", 1, "DoExecution")
		CPGG "#" 0 A_JumpIfInventory("Kicking",1,"DoKick")
		CPGG "#" 0 A_JumpIfInventory("Taunting",1,"Taunt")
		CPGG "#" 0 A_JumpIfInventory("Salute1", 1, "Salute")
		CPGG "#" 0 A_JumpIfInventory("Salute2", 1, "Salute")
		CPGG "#" 0 A_JumpIfInventory("Reloading",1,"Reload")
		CPGG "#" 0 A_JumpIfInventory("TossGrenade",1,"TossGrenade")		
		CPGG "#" 0 A_JumpIfInventory("Unloading",1,"Unload")
		CPGG "#" 0 A_JumpIfInventory("IsRunning",1,"StartSprint")
		CPGG "#" 0 A_JumpIfInventory("StartDualWield",1,"WeapSpecial")
		Loop

	User3:
	WeapSpecial:
		CPGG "#" 0 A_GiveInventory("PistolSpecial",1)
		CPGG "#" 0 A_GiveInventory("CryoRifle",1)
		CPGG "#" 0 A_Takeinventory("StartDualWield",1)
		CPGG "#" 0 A_SelectWeapon("CryoRifle")
		Goto Ready

	StartSprint:
		CPGG G 0 A_JumpIfInventory("BDPistolAmmo",1,"StartSprintLoaded")
		CPGG HIJ 1
		Goto StartSprintCheck
	StartSprintLoaded:
		CPGG CDE 1 A_WeaponReady(WRF_NOFIRE)
	StartSprintCheck:
		CPGG A 0 A_JumpIfInventory("UsedStamina", 40, "StopSprint")
	Sprinting:		
		CPGG G 0 A_JumpIfInventory("BDPistolAmmo",1,2)
		CPGG L 0
		CPGG "#" 0 offset(-9,32)
		//CPGG A 0 offset(-9,32) A_JumpIfInventory("IsStandingStill", 1, "Ready3")
		CPGG "#" 0 offset(-9,32) A_JumpIfInventory("UsedStamina", 0, "StopSprint")
		CPGG "#" 0 offset(-9,32) ACS_ExecuteAlways(852, 0, 0, 0, 0)//Makes player faster.
		CPGG "#" 0 offset(-9,32) A_JumpIfInventory("PowerStrength", 1, 2)
		CPGG "#" 0 offset(-9,32) A_GiveInventory("UsedStamina", 3)
		CPGG "#" 0 offset(-9,32)
		CPGG "#" 1 offset(-9,34) A_SetPitch(pitch -0.5)
		CPGG "#" 1 offset(-6,36) A_SetPitch(pitch -0.5)
		CPGG "#" 1 offset(-3,38) A_SetPitch(pitch -0.5)
		CPGG "#" 1 offset(0,38) A_SetPitch(pitch -0.5)
		CPGG "#" 1 offset(3,36) A_SetPitch(pitch -0.5)
		CPGG "#" 1 offset(6,34) A_SetPitch(pitch -0.5)
		CPGG "#" 1 offset(9,32) A_SetPitch(pitch -0.5)
		CPGG "#" 0 offset(9,32) A_WeaponReady(WRF_NOBOB)
		CPGG "#" 0 A_JumpIfInventory("Kicking",1,"DoKick")
		CPGG "#" 0 A_JumpIfInventory("Taunting",1,"Taunt")
		CPGG "#" 0 A_JumpIfInventory("Reloading",1,"Reload")
		CPGG "#" 0 offset(-9,32) A_GiveInventory("UsedStamina", 3)
		CPGG "#" 0 offset(9,33) A_SpawnItemEx("FootStep", 0, 0, 2, 0, 0, -4)
		CPGG "#" 1 offset(9,34) A_SetPitch(pitch +0.5)
		CPGG "#" 1 offset(6,36) A_SetPitch(pitch +0.5)
		CPGG "#" 1 offset(3,38) A_SetPitch(pitch +0.5)
		CPGG "#" 1 offset(0,38) A_SetPitch(pitch +0.5)
		CPGG "#" 1 offset(-3,36) A_SetPitch(pitch +0.5)
		CPGG "#" 1 offset(-6,34) A_SetPitch(pitch +0.5)
		CPGG "#" 1 offset(-9,32) A_SetPitch(pitch +0.5)
		CPGG "#" 0 offset(-9,32) A_WeaponReady(WRF_NOBOB)
		CPGG "#" 0 offset(-9,32) A_JumpIfInventory("IsRunning", 1, "Sprinting")
		Goto StopSprint
		
	StopSprint:
		CPGG G 0 A_JumpIfInventory("BDPistolAmmo",1,"StopSprintLoaded")
		CPGG JIH 1
		Goto StopSprintFinish
	StopSprintLoaded:
		CPGG EDC 1	
	StopSprintFinish:
		CPGG A 0 A_JumpIfInventory("BDPistolAmmo",1,2)
		CPGG B 0	
		CPGG "#" 0 A_JumpIfInventory("UsedStamina", 60, "StopSprintTired")
		PLAY A 0 ACS_ExecuteAlways(853, 0, 0, 0, 0)//Makes player slower.
		Goto Ready

	Deselect:
		CPGG A 0 A_TakeInventory("TossGrenade", 1)
		CPGG A 0 A_JumpIfInventory("PistolSpecial", 1, "DeselectInstant")
		CPGG A 0 A_JumpIfInventory("BDPistolAmmo",1,1)
		Goto DeselectNoAmmo
		CPGS IJKL 1
		TNT1 AAAAAAAAAAAAAAAAAAAAAAAA 0 A_Lower
		TNT1 A 1 A_Lower
		Wait

	DeselectNoAmmo:
		CPGS MNOP 1
		TNT1 AAAAAAAAAAAAAAAAAAAAAAAA 0 A_Lower
		TNT1 A 1 A_Lower
		Wait	

   	Fire:
		CPGG A 0 A_JumpIfInventory("NoReloading",1,"NoReload")
		CPGG A 0 A_JumpIfInventory("BDPistolAmmo",1,1)
		Goto NoAmmo
		CPGG A 0 A_Takeinventory("BDPistolAmmo",1)
		CPGG A 0 A_JumpIfInventory("BDPistolAmmo",6,2)
		CPGG A 0 A_PlaySound("weapons/lowammo",CHAN_AUTO)
		TNT1 AAA 0
		Goto FireContinueRight

	NoReload:
		CPGG A 0 A_JumpIfInventory("Clip1",1,1)
		Goto NoAmmo
		CPGG A 0 A_Takeinventory("Clip1",1,TIF_NOTAKEINFINITE)
		TNT1 AAA 0
	FireContinueRight:
		TNT1 A 0 A_AlertMonsters
		CPGG A 0 A_PlaySound("FARSHT", 6)
		CPGG A 0 A_PlaySound("Pistol/Fire", 1)
		CPGG A 0 A_JumpIFInventory("PistolBurstFire", 1, "Fire2")		
		CPGG A 0 A_ZoomFactor(0.98)
		CPGG A 0 A_FireCustomMissile("YellowFlareSpawn",0,0,0,0)
		CPGF A 1 BRIGHT A_FireBullets (-2, 1, -1, 14, "WeakPuff", FBF_NORANDOM)//Right
		CPGG A 0 A_ZoomFactor(1.0)
		RIFF A 0 A_FireCustomMissile("DecorativeTracer", 1, 0, 5, -2)
		CPGF B 1
		CPGG A 0 A_FireCustomMissile("PistolCaseSpawn",5,0,10,-4)
		CPGF CD 1
		CPGG A 5 A_WeaponReady(WRF_NOBOB | WRF_ALLOWRELOAD)
		TNT1 A 0 A_Refire
		Goto Ready

	Fire2:
		CPGG A 0 A_ZoomFactor(0.98)
		CPGG A 0 A_FireCustomMissile("YellowFlareSpawn",0,0,0,0)
		CPGF A 1 BRIGHT A_FireBullets (-4, 2, -1, 14, "WeakPuff", FBF_NORANDOM)//Right
		CPGG A 0 A_ZoomFactor(1.0)
		TNT1 A 0 A_SetPitch(pitch-1)
		CPGG A 0 A_GiveInventory("BurstCounter",1)
		RIFF A 0 A_FireCustomMissile("DecorativeTracer", 1, 0, 5, -2)
		CPGF B 1
		CPGG A 0 A_FireCustomMissile("PistolCaseSpawn",5,0,10,-4)
		CPGG A 0 A_JumpIfInventory("BurstCounter",3,2)	
		Goto Fire
		CPGG AAA 0
		CPGF C 1 A_TakeInventory("BurstCounter",0)
		CPGF D 1
		CPGG A 2 A_WeaponReady(WRF_NOBOB | WRF_ALLOWRELOAD)
		Goto Ready

	AltFire:
		TNT1 A 0
		TNT1 A 0 A_JumpIFInventory("PistolBurstFire", 1, "StopAlt")
		TNT1 A 0 A_GiveInventory("PistolBurstFire", 1)
		TNT1 A 0 A_Print("$BD_PISTOL_BURST_MODE",0.5)
		TNT1 A 0 A_PlaySound("Weapon/Empty", 5)
		CPGG "#" 1 offset(0,34)
		CPGG "#" 1 offset(0,33)
		Goto Ready3

	StopAlt:
		TNT1 A 0
		TNT1 A 0 A_TakeInventory("PistolBurstFire", 1)
		TNT1 A 0 A_Print("$BD_PISTOL_SINGLE_MODE",0.5)
		TNT1 A 0 A_PlaySound("Weapon/Empty", 5)
		CPGG "#" 1 offset(0,34)
		CPGG "#" 1 offset(0,33)
		Goto Ready3

	Reload:
		CPGG "#" 0 A_ZoomFactor(1.0)
		CPGG "#" 0 A_Takeinventory("Reloading",1)
		CPGG "#" 0 A_Takeinventory("Zoomed",1)
		CPGG "#" 0 A_Takeinventory("ADSmode",1)
		CPGG "#" 0 A_JumpIfInventory("NoReloading",1,"Ready")
		CPGG "#" 1 A_WEAPONREADY(WRF_NOBOB|WRF_NOFIRE|WRF_ALLOWZOOM)
		CPGG "#" 0 A_JumpIfInventory("BDPistolAmmo",0,"Ready")
		CPGG "#" 0 A_JumpIfInventory("Clip1",1,3)
		Goto Ready
		TNT1 AAA 0
		PIST E 0 A_Takeinventory("Zoomed",1)
		PIST E 0 A_ZoomFactor(1.0)
		PIST E 0 A_GiveInventory ("Pumping", 1)
		PIST E 0 A_Takeinventory("Reloading",1)
		PIST E 0 A_JumpIfInventory("HasUnloadedPistol", 1, 3)
		RIFR C 0 A_FireCustomMissile("EmptyClip2Spawn",-5,0,8,-4)
		PIST E 0 A_PlaySound("Pistol/Reload1", 5)
		PIST E 0 A_JumpIfInventory("BDPistolAmmo",1,"Reload2")//15+1 effect
		TNT1 AA 0
		CP6R ABCDEFGG 1 A_WEAPONREADY(WRF_NOBOB|WRF_NOFIRE|WRF_ALLOWZOOM)
		CP6R HHIJK 1 A_WEAPONREADY(WRF_NOBOB|WRF_NOFIRE|WRF_ALLOWZOOM)
		CPGR L 10 A_WEAPONREADY(WRF_NOBOB|WRF_NOFIRE|WRF_ALLOWZOOM)
		2ISR F 0 A_PlaySound("Pistol/Reload2", 5)
	InsertBullets:
		TNT1 AAAA 0		
		PIST E 0 A_JumpIfInventory("BDPistolAmmo",15,15)
		PIST E 0 A_JumpIfInventory("Clip1",1,3)
		Goto FinishReload
		TNT1 AAAAAA 0
		PIST E 0 A_Giveinventory("BDPistolAmmo",1)
		PIST E 0 A_Giveinventory("BDDualPistolAmmo",1)
		PIST E 0 A_Takeinventory("Clip1",1,TIF_NOTAKEINFINITE)
		Goto InsertBullets
		TNT1 AAAAAAAAAA 0
		PIST E 0 A_Takeinventory("Reloading",1)
		PIST E 0 //A_Refire
		Goto FinishReload
		TNT1 AAAA 0
		PIST E 0 A_Takeinventory("Reloading",1)
		PIST E 0 //A_Refire
		Goto FinishReload

	FinishReload:
		CPGR L 5 A_WEAPONREADY(WRF_NOBOB|WRF_NOFIRE|WRF_ALLOWZOOM)	
		CP6R LMNOO 1 A_WEAPONREADY(WRF_NOBOB|WRF_NOFIRE|WRF_ALLOWZOOM)
		"####" "#" 0 A_PlaySound("pistol/slide2", 5)
		CP6R PQRSSSTT 1 A_WEAPONREADY(WRF_NOBOB|WRF_NOFIRE|WRF_ALLOWZOOM)
		PIST E 0 A_Takeinventory("HasUnloadedPistol",1)
		PIST E 0 A_Takeinventory("HasUnloadedDualPistols",1)
		Goto Ready

	Reload2: //empty
		TNT1 AA 0
		CPGR ABCDEFGG 1 A_WEAPONREADY(WRF_NOBOB|WRF_NOFIRE|WRF_ALLOWZOOM)
		CPGR HIJKK 1 A_WEAPONREADY(WRF_NOBOB|WRF_NOFIRE|WRF_ALLOWZOOM)
		CPGR L 10 A_WEAPONREADY(WRF_NOBOB|WRF_NOFIRE|WRF_ALLOWZOOM)
		PIST E 0 A_PlaySound("Pistol/Reload2", 5)
	InsertBullets2:
		TNT1 AAAA 0
		PIST E 0 A_JumpIfInventory("BDPistolAmmo",0,15)
		PIST E 0 A_JumpIfInventory("Clip1",1,3)
		Goto FinishReload2
		TNT1 AAAAAA 0
		PIST E 0 A_Giveinventory("BDPistolAmmo",1)
		PIST E 0 A_Giveinventory("BDDualPistolAmmo",1)
		PIST E 0 A_Takeinventory("Clip1",1,TIF_NOTAKEINFINITE)
		Goto InsertBullets2
		TNT1 AAAAAAAAAA 0
		PIST E 0 A_Takeinventory("Reloading",1)
		PIST E 0 //A_Refire
		Goto FinishReload2
		TNT1 AAAA 0
		PIST E 0 A_Takeinventory("Reloading",1)
		PIST E 0 //A_Refire
		Goto FinishReload2

	FinishReload2:
		CPGR L 5 A_WEAPONREADY(WRF_NOBOB|WRF_NOFIRE|WRF_ALLOWZOOM)
		CPGS ABCD 1 A_WeaponReady(WRF_NOFIRE)
		CPGG A 0 A_Takeinventory("HasUnloadedPistol",1)
		CPGG A 0 A_Takeinventory("HasUnloadedDualPistols",1)
		Goto Ready

	User2:
	Unload:
		CPGG A 1 A_WeaponReady
		CPGG A 0 A_Takeinventory("Unloading",1)
		CPGG A 0 A_Takeinventory("ADSmode",1)
		CPGG A 0 A_JumpIfInventory("BDPistolAmmo",1,3)
		Goto Ready
		TNT1 AAA 0
		CPGG A 0 A_Takeinventory("Zoomed",1)
		CPGG A 0 A_ZoomFactor(1.0)
		CPGG A 0 A_GiveInventory ("Pumping", 1)
		CPGG A 0 A_Takeinventory("Reloading",1)
		CPGR ABCDEFGG 1 A_WEAPONREADY(WRF_NOBOB|WRF_NOFIRE|WRF_ALLOWZOOM)
		CPGR HIJKK 1 A_WEAPONREADY(WRF_NOBOB|WRF_NOFIRE|WRF_ALLOWZOOM)
		CPGR L 10 A_WEAPONREADY(WRF_NOBOB|WRF_NOFIRE|WRF_ALLOWZOOM)
		PIST F 0 A_PlaySound("Pistol/Reload1", 5)
	RemoveBullets:
		TNT1 AAAA 0
		CPGG A 0 A_JumpIfInventory("BDDualPistolAmmo",1,3)
		Goto FinishUnload
		TNT1 AAAAAA 0
		CPGG A 0 A_Takeinventory("BDPistolAmmo",1)
		CPGG A 0 A_Takeinventory("BDDualPistolAmmo",1)
		CPGG A 0 A_Giveinventory("Clip1",1)
		Goto RemoveBullets
		TNT1 AAAAAAAAAA 0
		CPGG A 0 A_Takeinventory("Reloading",1)
		CPGG A 0 A_Refire
		Goto FinishUnload
		TNT1 AAAA 0
		CPGG A 0 A_Takeinventory("Reloading",1)
		CPGG A 0 A_Refire
		Goto FinishUnload
	FinishUnload:	
		CPGR L 5 A_WEAPONREADY(WRF_NOBOB|WRF_NOFIRE|WRF_ALLOWZOOM)
		CPGS EFGH 1 A_WeaponReady(WRF_NOFIRE)
		CPGG A 0 A_Giveinventory("HasUnloadedPistol",1)
		CPGG A 0 A_Giveinventory("HasUnloadedDualPistols",1)
		Goto Ready

	Spawn:
		P3ST K -1
		Stop

	Steady:
		TNT1 A 1
		Goto Ready
	}
}
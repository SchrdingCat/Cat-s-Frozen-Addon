ACTOR CryoRifle : BrutalWeapon
{
	Weapon.AmmoType1 "AmmoCell"
	Weapon.AmmoType2 "CryoAmmo"
	Weapon.SelectionOrder 100
	-WEAPON.NOAUTOFIRE
	-INVENTORY.UNDROPPABLE
	Inventory.PickupSound "Plasma/Draw"
	Inventory.PickupMessage "You've got the Cryo Rifle"
	Tag "Cryo Rifle"
	Obituary "\cr%o\c- got frozen by \cr%k's \c-Cryo Rifle."
	States
	{
	KickFlash: 
		CYGW F 0 A_JumpIfInventory("PistolSpecial",1,"KickFlashPistol")
		CYGW BCDE 1
		CYGW F 10
		CYGW EDCB 1
		stop
	SlideFlash: 
		CYGW F 0 A_JumpIfInventory("PistolSpecial",1,"KickFlashPistol")
		2RSG A 0 A_JumpIfInventory("IsPlayingAsPurist", 1,"PuristSlideFlash")
		CYGW BCDE 1
		CYGW F 35
		stop
	SlideFlashEnd: 
		CYGW F 0 A_JumpIfInventory("PistolSpecial",1,"KickFlashPistol")
		2RSG A 0 A_JumpIfInventory("IsPlayingAsPurist", 1,"PuristSlideFlashEnd")
		CYGW F 5
		CYGW EDCB 1
		stop
	SlideFlashEndEarly: 
		CYGW F 0 A_JumpIfInventory("PistolSpecial",1,"KickFlashPistol")
		2RSG A 0 A_JumpIfInventory("IsPlayingAsPurist", 1,"PuristSlideFlashEndEarly")
		CYGW F 3
		CYGW EDCB 1
		stop
		
	Deselect:
		CYG2 F 0 A_Takeinventory("Reloading",1)
		CYG2 F 0 A_Takeinventory("HasFreezerWeapon",1)
		CYG2 F 0 A_TakeInventory("TossGrenade", 1)
		CYG2 F 0 A_PlaySound("Plasma/BeepLow", 5)
		TNT1 A 0 A_JumpIfInventory("PistolSpecial", 1, "DeselectInstant")
		CYGW A 0 A_JumpIfInventory("IsPlayingAsPurist", 1,"DeselectPurist")
		CYGS EFGH 1
		TNT1 AAAAAAAAAAAAAAAAAAAAAA 0 A_Lower
		TNT1 A 1 A_Lower
		Wait

	Select:
		TNT1 A 0 A_Giveinventory("GoSpecial",1)
		TNT1 A 0 A_GiveInventory("TokenCleaner")
		TNT1 F 0 A_Giveinventory("HasFreezerWeapon",1)
		//TNT1 F 0 A_Giveinventory("PlasmaGunSelected",1)
		TNT1 A 0 A_SetCrosshair( CallACS("GetCrosshair", 10) )
		TNT1 A 0 A_TakeInventory("PistolFromCryoRifle", 1)
		TNT1 A 0 A_JumpIfInventory("PistolSpecial", 1,"SelectCryoRifle")
		TNT1 A 1
		TNT1 F 0 A_JumpIfInventory("IsRunning",1,"StartSprint")
		TNT1 A 0 A_JumpIfInventory("NeverSelectedCryo", 0, "FirstSelectAnimation")
		TNT1 F 0 A_PlaySound("Plasma/Draw", 5)
	SelectAnimation:
		CYGW A 0 A_JumpIfInventory("IsPlayingAsPurist", 1,"SelectPurist")
		CYGS ABCD 1 A_WeaponReady(WRF_NOFIRE)
		TNT1 F 0 A_Takeinventory("StartDualWield",1)
		Goto Ready

	SelectCryoRifle:
		CPGG A 0 A_Takeinventory("PistolSpecial",1)
		CPGG A 0 A_JumpIfInventory("BDPistolAmmo",1,1)
		Goto SelectCryoRifleFromEmptyPistol
		CPGS DCBA 1 A_WeaponReady(WRF_NOFIRE)
		Goto FinishSelectCryoRifle
	SelectCryoRifleFromEmptyPistol:	
		CPGS HGFE 1 A_WeaponReady(WRF_NOFIRE)
	FinishSelectCryoRifle:	
		TNT1 F 0 A_PlaySound("Plasma/Draw", 5)
		CYGW FEDCB 1 A_WeaponReady(WRF_NOFIRE)
		CYGW A 0 A_Takeinventory("StartDualWield",1)
		Goto Ready


	FirstSelectAnimation:
		TNT1 A 0 A_TakeInventory("NeverSelectedCryo",1)
		CYG2 F 0 A_JumpIfInventory("IsRunning",1,"StartSprint")
		TNT1 A 3 A_WeaponReady(WRF_ALLOWRELOAD|WRF_ALLOWZOOM)
		CYG3 VUTSA 1 A_WeaponReady(WRF_ALLOWRELOAD|WRF_ALLOWZOOM)
		CYG3 A 6 A_WeaponReady(WRF_ALLOWRELOAD|WRF_ALLOWZOOM)
		CYG3 BCD 1 A_WEAPONREADY(WRF_NOBOB|WRF_NOFIRE|WRF_ALLOWZOOM)
		CYG3 F 0 A_PlaySound("Plasma/Reload2", 5)
		CYG3 EFG 1 A_WEAPONREADY(WRF_NOBOB|WRF_NOFIRE|WRF_ALLOWZOOM)
		CYG3 F 0 A_PlaySound("FRZIDLE", 5)
		CYG3 HIJKLMHIJKLMHIJKLMHIJKLM 2 A_WEAPONREADY(WRF_NOBOB|WRF_NOFIRE|WRF_ALLOWZOOM)
		CYG3 F 0 A_StopSound(5)
		CYG3 F 0 A_PlaySound("Plasma/Reload1", 5)
		CYG3 NOPQR 1 A_WeaponReady(WRF_ALLOWRELOAD|WRF_ALLOWZOOM)
		CYG3 A 5 A_WeaponReady(WRF_ALLOWRELOAD|WRF_ALLOWZOOM)
		tnt1 a 0 A_Takeinventory("Reloading",1)
		CYG3 STUV 1 A_WeaponReady(WRF_ALLOWRELOAD|WRF_ALLOWZOOM)
	Ready3:
	Ready:
		CYGW A 0 A_JumpIfInventory("Kicking",1,"DoKick")
		CYGW A 0 A_JumpIfInventory("Taunting",1,"Taunt")
		CYGW A 0 A_JumpIfInventory("Salute1", 1, "Salute")
		CYGW A 0 A_JumpIfInventory("Salute2", 1, "Salute")
		CYGW A 0 A_JumpIfInventory("Reloading",1,"Reload")
		CYGW A 0 A_JumpIfInventory("TossGrenade",1,"TossGrenade")
		CYGW A 0 A_JumpIfInventory("IsRunning",1,"StartSprint")
		CYGW A 0 A_JumpIfInventory("Unloading",1,"Unload")
		CYGW A 0 A_JumpIfInventory("StartDualWield",1,"WeapSpecial")
		CYGW A 1 A_WeaponReady
		Loop

	StartSprint:
		TNT1 A 0 A_TakeInventory("NeverSelectedCryo",1)
		CYGW A 0 A_JumpIfInventory("UsedStamina", 40, "StopSprintTired")
		CYGW BCDE 1 A_WeaponReady(WRF_NOFIRE)
		CYGW A 0 A_Takeinventory("Zoomed",1)
		CYGW A 0 A_Takeinventory("ADSmode",1)
		CYGW A 0 A_ZoomFactor(1.0)	
		Goto Sprinting
	Sprinting:
		CYGW A 0 offset(-9,32)
		//PRSG F 0 offset(-9,32) A_JumpIfInventory("IsStandingStill", 1, "Ready3")
		CYGW F 0 offset(-9,32) A_JumpIfInventory("UsedStamina", 0, "StopSprint")
		CYGW A 0 offset(-9,32) ACS_ExecuteAlways(852, 0, 0, 0, 0)//Makes Player faster.
		CYGW F 0 offset(-9,32) A_JumpIfInventory("PowerStrength", 1, 2)
		CYGW F 0 offset(-9,32) A_GiveInventory("UsedStamina", 4)
		CYGW F 0 offset(-9,32)
		CYGW F 0
		CYGW F 1 offset(-9,34) A_SetPitch(pitch -0.5)
		CYGW F 1 offset(-6,36) A_SetPitch(pitch -0.5)
		CYGW F 1 offset(-3,38) A_SetPitch(pitch -0.5)
		CYGW F 1 offset(0,38) A_SetPitch(pitch -0.5)
		CYGW F 1 offset(3,36) A_SetPitch(pitch -0.5)
		CYGW F 1 offset(6,34) A_SetPitch(pitch -0.5)
		CYGW F 1 offset(9,32) A_SetPitch(pitch -0.5)
		CYGW F 0 offset(9,32) A_WeaponReady(WRF_NOBOB)
		CYGW F 0 A_JumpIfInventory("Kicking",1,"DoKick")
		CYGW F 0 A_JumpIfInventory("Taunting",1,"Taunt")
		CYGW F 0 A_JumpIfInventory("Reloading",1,"Reload")
		CYGW F 0 offset(-9,32) A_GiveInventory("UsedStamina", 4)
		CYGW F 0 offset(9,33) A_SpawnItemEx("FootStep", 0, 0, 2, 0, 0, -4)
		CYGW F 1 offset(9,34) A_SetPitch(pitch +0.5)
		CYGW F 1 offset(6,36) A_SetPitch(pitch +0.5)
		CYGW F 1 offset(3,38) A_SetPitch(pitch +0.5)
		CYGW F 1 offset(0,38) A_SetPitch(pitch +0.5)
		CYGW F 1 offset(-3,36) A_SetPitch(pitch +0.5)
		CYGW F 1 offset(-6,34) A_SetPitch(pitch +0.5)
		CYGW F 1 offset(-9,32) A_SetPitch(pitch +0.5)
		CYGW F 0 offset(-9,32) A_WeaponReady(WRF_NOBOB)
		CYGW F 0 offset(-9,32) A_JumpIfInventory("IsRunning", 1, "Sprinting")
		Goto StopSprint

	StopSprint:
		CYGW EDCB 1
		PRSG F 0 A_JumpIfInventory("UsedStamina", 60, "StopSprintTired")
		PLAY A 0 ACS_ExecuteAlways(853, 0, 0, 0, 0)//Makes Player slower.
		Goto Ready

	Fire:
		CYGW A 0 A_JumpIfInventory("IsPlayingAsPurist", 1,"NoReload")
		CYGW A 0 A_JumpIfInventory("NoReloading",1,"NoReload")
		CYGW A 0 A_JumpIfInventory("CryoAmmo",1,1)
		Goto NoAmmo
		TNT1 AAA 0
		//"####" F 0 A_Takeinventory("DoublePlasmaAmmo",1)
		CYGW A 0 A_Takeinventory("CryoAmmo",1)
		Goto ResumeFireFrame

	User3:
	WeapSpecial:
		"####" "#" 0 A_Takeinventory("StartDualWield",1)
		"####" "#" 0 A_JumpIfInventory("BrutalPistol", 1,1)
		goto PistolWarning
		"####" "#" 0 A_JumpIfInventory("IsPlayingAsPurist", 1,"PuristWarning")
	GetWeapSpecial:
		CYGW A 0 A_Takeinventory("Zoomed",1)
		CYGW A 0 A_Takeinventory("ADSmode",1)
		CYGW A 0 A_TakeInventory("TossGrenade", 1)
		CYGW A 0 A_TakeInventory("Kicking",1)
		CYGW A 0 A_TakeInventory("Taunting",1)
		CYGW A 0 A_TakeInventory("Reloading",1)
		CYGW A 0 A_TakeInventory("Unloading",1)
		CYGW A 0 A_TakeInventory("Salute1", 1)
		CYGW A 0 A_TakeInventory("Salute2", 1)
		CYGW A 0 A_ZoomFactor(1.0)
		CYGW A 0 A_GiveInventory("PistolSpecial",1)
		CYGW A 0 A_GiveInventory("PistolFromCryoRifle",1)
		CYGW A 0 A_SelectWeapon("PistolFromCryoRifle")
		Goto Ready
	PistolWarning:
		"####" "#" 0 A_TakeInventory("FiredSecondary",1)
		"####" "#" 0 A_TakeInventory("Reloading",1)
		"####" "#" 0 A_TakeInventory("Unloading",1)
		"####" "#" 0 A_Print("You must have a pistol to quick draw.",3)
		"####" "##" 0
		"####" "#" 1 A_Raise
		Goto Ready3		
	NoReload:
		CYGW A 0 A_Takeinventory("Reloading",1)
		CYGW A 0 A_JumpIfInventory("AmmoCell",1,2)
		Goto NoAmmo
		TNT1 AAA 0
		CYGW A 0 A_Takeinventory("AmmoCell",1,TIF_NOTAKEINFINITE)
	ResumeFireFrame:
		TNT1 AAAA 0
		TNT1 A 0 A_AlertMonsters
		TNT1 F 0 A_PlaySound("cryoshot", 1)
		CYGW A 0 A_ZoomFactor(0.99)
		CYGW A 0 A_JumpIfInventory("DMgame", 1, "FireDM")
		CYGF A 1 BRIGHT A_FireCustomMissile("BigIcicle2", 0, 1, 0, -5)
		CYGF BCD 1
		Goto FinishFire
	FireDM:
		CYGF A 1 BRIGHT A_FireCustomMissile("BigIcicle2", 0, 1, 0, -5)
	FinishFire:
		TNT1 A 0 A_ZoomFactor(1.0)
		TNT1 A 0 A_Quake(1,2,0,12,"")
		CYGF FG 1
		CYGW A 1 A_Refire
		Goto Ready

	Cooldown:
		CYG3 VUTSABCD 1
		CYG3 EFGHIJKLMNOPQR 1 A_FireCustomMissile("SmokeSpawner",0,0,10,5)
		CYG3 STUV 1
		Goto Ready


// Charging up secondary ------------------------------------------------------------

	AltFire:
		TNT1 A 0 A_JumpIfInventory("PistolSpecial",1,"FirePistol")
		CYGU A 0 A_JumpIfInventory("IsPlayingAsPurist", 1,"PuristWarning")
		CYGU A 0 A_JumpIfInventory("NoReloading",1,"AltNoReload")
		CYGW A 0 A_JumpIfInventory("CryoAmmo",15,1)
		Goto NoAmmo
		TNT1 AAA 0
		CYGW A 0 A_GiveInventory("CryoCharging",1)
		TNT1 A 0 A_AlertMonsters
		CYGF G 0 A_PlaySound("Plasma/Reload2")
		CYGW A 6 A_PlaySound("cryosta", 5)
		CYGF H 1 BRIGHT A_FireCustomMissile("CryoFlareSpawn",0,0,0,0)
		CYGW A 0 BRIGHT A_Takeinventory("CryoAmmo",1)
		CYGF I 1 BRIGHT A_FireCustomMissile("CryoFlareSpawn",0,0,0,0)
		CYGF J 1 BRIGHT 
		CYGW A 0 BRIGHT A_Takeinventory("CryoAmmo",1)
		CYGF K 1 BRIGHT A_FireCustomMissile("CryoFlareSpawn",0,0,0,0)
		CYGF L 1 BRIGHT 
		CYGW A 0 BRIGHT A_Takeinventory("CryoAmmo",1)
		CYGF M 1 BRIGHT A_FireCustomMissile("CryoFlareSpawn",0,0,0,0)
		CYGF N 1 BRIGHT A_Takeinventory("CryoAmmo",1)
		CYGF O 1 BRIGHT A_FireCustomMissile("CryoFlareSpawn",0,0,0,0)
		CYGF P 1 BRIGHT 
		CYGW A 0 BRIGHT A_Takeinventory("CryoAmmo",1)
		CYGF Q 1 BRIGHT A_FireCustomMissile("CryoFlareSpawn",0,0,0,0)
		CYGF R 1 BRIGHT 
		CYGW A 0 BRIGHT A_Takeinventory("CryoAmmo",1)
		CYGF N 1 BRIGHT A_FireCustomMissile("CryoFlareSpawn",0,0,0,0)
		CYGF O 1 BRIGHT A_Takeinventory("CryoAmmo",1)
		CYGF P 1 BRIGHT A_FireCustomMissile("CryoFlareSpawn",0,0,0,0)
		CYGW A 0 BRIGHT A_Takeinventory("CryoAmmo",1)
		CYGF Q 1 BRIGHT offset (1, 33)
		CYGF R 1 BRIGHT A_FireCustomMissile("CryoFlareSpawn",0,0,0,0)
		CYGW A 0 BRIGHT A_Takeinventory("CryoAmmo",1)
		CYGF N 1 BRIGHT A_FireCustomMissile("CryoFlareSpawn",0,0,0,0)
		CYGF O 1 BRIGHT offset (-1, 32)
		CYGW A 0 BRIGHT A_Takeinventory("CryoAmmo",1)
		CYGF P 1 BRIGHT A_FireCustomMissile("CryoFlareSpawn",0,0,0,0)
		CYGW A 0 BRIGHT A_Takeinventory("CryoAmmo",1)
		CYGF Q 1 BRIGHT offset (0, 32) 
		CYGF R 1 BRIGHT A_FireCustomMissile("CryoFlareSpawn",0,0,0,0)
		CYGW A 0 BRIGHT A_Takeinventory("CryoAmmo",1)
		CYGF O 1 BRIGHT offset (0, 32) 
		CYGF P 1 BRIGHT A_FireCustomMissile("CryoFlareSpawn",0,0,0,0)
		CYGW A 0 BRIGHT A_Takeinventory("CryoAmmo",1)
		CYGF Q 1 BRIGHT A_FireCustomMissile("CryoFlareSpawn",0,0,0,0)
		CYGF R 1 BRIGHT offset (0, 33)
		CYGW A 0 BRIGHT A_Takeinventory("CryoAmmo",1)
		CYGF O 1 BRIGHT A_FireCustomMissile("CryoFlareSpawn",0,0,0,0)
		CYGW A 0 BRIGHT 
		CYGW A 0 A_PlaySound("cryoloo", 6, 0.8, 1)
		CYGW A 0 A_ReFire

// Holding Charge ------------------------------------------------------------
	AltHold:
		TNT1 A 0 A_JumpIfInventory("PistolSpecial",1,"FirePistol")
		CYGW A 0 A_PlaySound("ALERT", 5)
		TNT1 A 0 A_AlertMonsters
		CYGW A 0 A_FireCustomMissile("CryoFlareSpawn",0,0,0,0)
		CYGF N 1 BRIGHT offset (0, 32) A_JumpIfInventory("Reloading",1,"Cancel")
		CYGW A 0 A_FireCustomMissile("CryoFlareSpawn",0,0,0,0)
		CYGF O 1 BRIGHT offset (0, 33) A_JumpIfInventory("Reloading",1,"Cancel")
		CYGW A 0 A_FireCustomMissile("CryoFlareSpawn",0,0,0,0)
		CYGF P 1 BRIGHT offset (1, 33) A_JumpIfInventory("Reloading",1,"Cancel")
		CYGW A 0 A_FireCustomMissile("CryoFlareSpawn",0,0,0,0)
		CYGF Q 1 BRIGHT offset (-1, 32) A_JumpIfInventory("Reloading",1,"Cancel")
		CYGW A 0 A_FireCustomMissile("CryoFlareSpawn",0,0,0,0)
		CYGF R 1 BRIGHT offset (0, 32) A_JumpIfInventory("Reloading",1,"Cancel")		
		CYGW A 0 A_ReFire
		Goto LetFly

// Let Fly --------------------------------------------------------------------------
	LetFly:
		CYGW A 0 A_TakeInventory("CryoCharging",1)
		CYGW A 0 A_StopSound(6)
		CYGF A 1 BRIGHT A_PlaySound("cryoball", 1)
		TNT1 A 0 A_AlertMonsters
		CYGW A 0 A_ZoomFactor(0.9)
		CYGU B 1 BRIGHT A_Recoil(3)
		CYGW A 0 A_StopSound(6)
		CYGW A 0 A_ZoomFactor(1.0)
		TNT1 A 0 A_JumpIfInventory("DMGame", 1, "LetFlyDM")
		CYGW A 0 A_FireCustomMissile("IceShard0", -8, 0, -1, 0, 0, random(-2,2))
		CYGW A 0 A_FireCustomMissile("IceShard0", -6, 0, -1, 0, 0, random(-2,2))
		CYGW A 0 A_FireCustomMissile("IceShard0", -4, 0, -1, 0, 0, random(-2,2))
		CYGW A 0 A_FireCustomMissile("IceShard0", -2, 0, -1, 0, 0, random(-2,2))
		CYGW A 0 A_FireCustomMissile("Icebuckshot", 0, 0, -1, 0, 0, 0)
		CYGW A 0 A_FireCustomMissile("IceShard0", 1, 0, -1, 0, 0, random(-2,2))
		CYGW A 0 A_FireCustomMissile("IceShard0", 2, 0, -1, 0, 0, random(-2,2))
		CYGW A 0 A_FireCustomMissile("IceShard0", 4, 0, -1, 0, 0, random(-2,2))
		CYGW A 0 A_FireCustomMissile("IceShard0", 6, 0, -1, 0, 0, random(-2,2))
		CYGW A 0 A_FireCustomMissile("IceShard0", 8, 0, -1, 0, 0, random(-2,2))
		CYGW A 0 A_FireCustomMissile("CryoFlareSpawn",-5,0,0,0)
		CYGF CD 1 BRIGHT A_SetPitch(pitch - 6.0)
		CYGF EFG 1 A_SetPitch(pitch - 2.0)	
		CYGF G 0 A_PlaySound("Plasma/Reload2", 6)
		CYGW GGHIJKL 1 A_WEAPONREADY(WRF_NOBOB|WRF_NOFIRE|WRF_ALLOWZOOM)
		CYGW F 0 A_PlaySound("blizzlo", 5)
		CYGW MNOPQRSMNOPQRSMNOPQRSMNOPQRSMNOPQRSMNOPQRS 1 A_WEAPONREADY(WRF_NOBOB|WRF_NOFIRE|WRF_ALLOWZOOM)
		CYGW TUV 1 A_PlaySound("Plasma/Reload1", 5)
		CYGW XGG 1 A_WeaponReady(WRF_ALLOWRELOAD|WRF_ALLOWZOOM)
		CYGW A 0 A_PlaySound("Plasma/BeepBeep", 5)
		CYGW A 0 A_StopSound(5)
		Goto Ready

// Cancel Charge --------------------------------------------------------------------------------------------
	Cancel:
		CYGW A 0 A_TakeInventory("CryoCharging",1)
		CYGW A 0 A_JumpIfInventory("NoReloading",1,"CancelNoReload")
		CYGF N 1 BRIGHT A_PlaySound("Plasma/Cool", 6)
		CYGF M 1 BRIGHT A_FireCustomMissile("CryoFlareSpawn",0,0,0,0)
		CYGW A 0 BRIGHT A_Giveinventory("CryoAmmo",2)
		CYGF L 1 BRIGHT A_FireCustomMissile("CryoFlareSpawn",0,0,0,0)
		CYGF K 1 BRIGHT A_FireCustomMissile("CryoFlareSpawn",0,0,0,0)
		CYGW A 0 BRIGHT A_Giveinventory("CryoAmmo",2)
		CYGF J 1 BRIGHT A_FireCustomMissile("CryoFlareSpawn",0,0,0,0)
		CYGF I 1 BRIGHT A_FireCustomMissile("CryoFlareSpawn",0,0,0,0)
		CYGW A 0 BRIGHT A_Giveinventory("CryoAmmo",2)
		CYGF H 1 BRIGHT A_FireCustomMissile("CryoFlareSpawn",0,0,0,0)
		CYGW A 0 A_TakeInventory("Reloading",1)
		CYGW A 0 A_PlaySound("Plasma/BeepBeep", 5, 1.2)
		CYGW A 0 A_ClearReFire
		Goto Ready

	Spawn:
		CRYR A -1
		Stop

	Reload:
		CYGW A 0 A_Takeinventory("Reloading",1)
		CYGU A 0 A_JumpIfInventory("PistolSpecial", 1,"PistolReload")		
		//CYGW A 0 A_ClearReFire
		CYGU A 0 A_JumpIfInventory("IsPlayingAsPurist", 1,"PuristWarningManual")		
		CYGW A 0 A_JumpIfInventory("CryoCharging",1,"Cancel")
		CYGW A 0 A_JumpIfInventory("CryoAmmo",0,"Ready")
		CYGW A 0 A_JumpIfInventory("AmmoCell",1,2)
		Goto Ready
		TNT1 AAAA 0
		CYGW A 0 A_JumpIfInventory("TurboReload",1,"TurboReload")
		TNT1 A 0 A_JumpIfInventory("HasUnloadedCryo",1,3)
		CYGW A 0 A_PlaySound("Plasma/Reload1", 5)
		CYGW A 0 A_FireCustomMissile("PlasmaCaseSpawn",-210,0,20,-20)		
		CYGR AABCDEFGHH 1 A_WEAPONREADY(WRF_NOBOB|WRF_NOFIRE|WRF_ALLOWZOOM)
		CYGR I 10 A_WEAPONREADY(WRF_NOBOB|WRF_NOFIRE|WRF_ALLOWZOOM)
		CYGR JKKLLMNN 1 A_WEAPONREADY(WRF_NOBOB|WRF_NOFIRE|WRF_ALLOWZOOM)
		CYGW A 0 A_PlaySound("Plasma/Reload2", 5)
	ReloadingSequence:
		TNT1 AAAAAAAAAA 0 
		CYGW A 0 A_JumpIfInventory("CryoAmmo",0,15)
		CYGW A 0 A_JumpIfInventory("AmmoCell",1,3)
		Goto FinishReload
		TNT1 AAAAAA 0
		CYGW A 0 A_Giveinventory("CryoAmmo",1)
		//CYGW A 0 A_Giveinventory("DoublePlasmaAmmo",1)
		CYGW A 0 A_Takeinventory("AmmoCell",1,TIF_NOTAKEINFINITE)
		Goto ReloadingSequence
		TNT1 AAAAAAAA 0

	FinishReload:
		CYGR OPQRRRR 1 A_WEAPONREADY(WRF_NOBOB|WRF_NOFIRE|WRF_ALLOWZOOM)
		CYGR STUU 1 A_WEAPONREADY(WRF_NOBOB|WRF_NOFIRE|WRF_ALLOWZOOM)
		tnt1 a 0 A_Takeinventory("Reloading",1)
		CYGR GFEDCBAA 1  A_WEAPONREADY(WRF_NOBOB|WRF_NOFIRE|WRF_ALLOWZOOM)
		CYGU A 0 A_Takeinventory("HasUnloadedCryo",1)
		Goto Ready

	TurboReload:
		PRSN BCDE 0
		Goto ReloadingSequence
		
	Unload:
		CYG2 F 0 A_Takeinventory("Unloading",1)
		CYG2 A 0 A_JumpIfInventory("IsPlayingAsPurist", 1,"PuristWarning")
		CYG2 F 0 A_JumpIfInventory("NoReloading",1,"Ready")
		CYG2 F 0 A_JumpIfInventory("CryoAmmo",1,2)
		Goto Ready
		TNT1 AAA 0
		CYGR AABCDEFGUTSRRRR 1 A_WEAPONREADY(WRF_NOBOB|WRF_NOFIRE|WRF_ALLOWZOOM)
		CYG2 F 0 A_PlaySound("Plasma/Reload1", 5)
	UnloadingSequence:
		CYG2 F 0
		CYG2 F 0 A_Takeinventory("Unloading",1)
		CYG2 F 0 A_JumpIfInventory("CryoAmmo",1,3)
		Goto FinishUnload
		TNT1 AAAAAAA 0
		CYG2 F 0 A_Takeinventory("CryoAmmo",1)
		CYG2 F 0 A_Takeinventory("DoublePlasmaAmmo",1)
		CYG2 F 0 A_Giveinventory("AmmoCell",1)
		Goto UnloadingSequence

	FinishUnload:	
		CYGR NNNMLLKJJIIIII 1
		CYGR HHGFEDCBAA 1
		PRSG F 0 A_Takeinventory("Unloading",1)
		PRSG F 0 A_Giveinventory("HasUnloadedCryo",1)
		Goto Ready
	}
}

ACTOR InvisibleCryoAmmo: CustomInventory
{
	scale 1.0
	radius 16
	height 4
	damage 0
	Mass 1
	Inventory.PickupMessage "CryoCells."
	States
	{
	Spawn:
		CELL D -1
		Stop

	Pickup:
		PLSG F 0
		PLSG F 0 A_GiveInventory("AmmoCell", 50)
		Stop
	}
}

ACTOR CryoAmmo : Ammo
{
	Inventory.Amount 0
	Inventory.MaxAmount 20
	Ammo.BackpackAmount 0
	Ammo.BackpackMaxAmount 20
	Inventory.Icon "CRYRA0"
}